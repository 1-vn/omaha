# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this file,
# You can obtain one at http://mozilla.org/MPL/2.0/.

import("//onevn/build/config.gni")

assert(is_win)

group("omaha") {
  deps = [ ":build_omaha_installer" ]
}

action("build_omaha_installer") {
  script="build_omaha.py"

  inputs = [ "build_omaha.py" ]

  sources = [ "$root_out_dir/$onevn_installer_exe" ]

  _install_switch = ""
  _tag_app_name = "Onevn-Release"
  if (onevn_channel == "beta") {
    _install_switch = "--chrome-beta"
    _tag_app_name = "Onevn-Browser-Beta"
  } else if (onevn_channel == "dev") {
    _install_switch = "--chrome-dev"
    _tag_app_name = "Onevn-Browser-Dev"
  } else if (onevn_channel == "nightly") {
    _install_switch = "--chrome-sxs"
    _tag_app_name = "Onevn-Browser-Nightly"
  } else {
    assert(onevn_channel == "", "Unknown channel name")
  }

  out_dir = rebase_path(root_out_dir)

  args = [
    "--root_out_dir=$out_dir",
    "--onevn_installer_exe=$onevn_installer_exe",
    "--stub_installer_exe=$onevn_stub_installer_exe",
    "--stub_silent_exe=$onevn_silent_stub_installer_exe",
    "--stub_untagged_exe=$onevn_untagged_stub_installer_exe",
    "--standalone_installer_exe=$onevn_standalone_installer_exe",
    "--silent_installer_exe=$onevn_silent_installer_exe",
    "--untagged_installer_exe=$onevn_untagged_installer_exe",
    "--guid=$onevn_app_guid",
    "--install_switch=$_install_switch",
    "--tag_ap=$tag_ap",
    "--tag_app_name=$_tag_app_name",
    "--onevn_full_version=$chrome_version_major.$onevn_version_major.$onevn_version_minor.$onevn_version_build",
  ]

  outputs = [
    "$root_out_dir/$onevn_stub_installer_exe",
    "$root_out_dir/$onevn_standalone_installer_exe",
    "$root_out_dir/$onevn_silent_installer_exe",
    "$root_out_dir/$onevn_untagged_installer_exe",
  ]

  deps = [
    "//onevn/build/win:create_signed_installer",
  ]
}

